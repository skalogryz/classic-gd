; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Classic gamedev.ru"
#define MyAppVersion "0.9.2"
#define MyAppPublisher "skalogryz"
#define MyAppURL "http://www.github.com/skalogryz/classic-gd/wiki"
; let's make it xampp based initially
#define ApacheDir = "D:\xampp\apache"
#define PhpDir = "D:\xampp\php"
#define ClassicGDSrcDir = "..\src"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{02726CC4-21F5-417C-9D53-B8B89063632C}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\classicgamedev
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
OutputBaseFilename=setup-classicgd
Compression=lzma
SolidCompression=yes

[Languages]
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"

[Files]
;todo: smaller installation, the default takes 169Mb
Source: "{#ApacheDir}\*"; DestDir: "{app}\apache"; Flags: ignoreversion 
Source: "{#ApacheDir}\bin\*"; DestDir: "{app}\apache\bin"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#ApacheDir}\error\*"; DestDir: "{app}\apache\error"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#ApacheDir}\icons\*"; DestDir: "{app}\apache\icons"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#ApacheDir}\lib\*"; DestDir: "{app}\apache\lib"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#ApacheDir}\modules\*"; DestDir: "{app}\apache\modules"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#ApacheDir}\conf\*"; DestDir: "{app}\apache\conf"; Flags: ignoreversion; Excludes: "httpd.conf,openssl.cnf";
Source: ".\httpd.conf"; DestDir: "{app}\apache\conf"; Flags: ignoreversion; AfterInstall: UpdateApacheConfig;
Source: ".\index.html"; DestDir: "{app}\apache\htdocs\"; Flags: ignoreversion; 
Source: "{#PhpDir}\*"; DestDir: "{app}\php"; Flags: ignoreversion recursesubdirs createallsubdirs; Excludes: "php.ini"
Source: ".\php.ini"; DestDir: "{app}\php\"; Flags: ignoreversion; AfterInstall: UpdatePhpConfig;
Source: "{#ClassicGDSrcDir}\*"; DestDir: "{app}\src"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
[Dirs]
Name: "{app}\apache\logs"

[Icons]
Name: "{group}\Classic Gamedev.ru"; Filename: "http://localhost:8088/gamedev.ru"
Name: "{group}\Apache Monitor"; Filename: "{app}\apache\bin\ApacheMonitor.exe"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"

[Run]
Filename: "{app}\apache\bin\httpd.exe"; Parameters: "-k install -n ClassicGDServer"
Filename: "{app}\apache\bin\httpd.exe"; Parameters: "-k start -n ClassicGDServer"
Filename: "http://localhost:8088/gamedev.ru/"; Flags: shellexec runasoriginaluser postinstall; Description: "запустить классический gamedev.ru (http://localhost:8088/gamedev.ru)"; 
                                                                                                                                               
[UninstallRun]
Filename: "{app}\apache\bin\httpd.exe"; Parameters: "-k stop -n ClassicGDServer"
Filename: "{app}\apache\bin\httpd.exe"; Parameters: "-k uninstall -n ClassicGDServer"

[Code]

procedure UpdateApacheConfig;
var
  fs : TStringList;
  i  : integer;
  s  : string;
  apacheroot : string;
  docroot   : string;
  instdir   : string;
  fn : string;
begin
  try
    fs := TStringList.Create;
    try
      instdir :=  ExpandConstant('{app}');
      StringChangeEx(instdir, '\', '/', True);

      apacheroot :=  ExpandConstant('{app}\apache');
      StringChangeEx(apacheroot, '\', '/', True);

      docroot :=  ExpandConstant('{app}\apache\htdocs');
      StringChangeEx(docroot, '\', '/', True);


      fn := ExpandConstant(CurrentFileName);
      fs.LoadFromFile(fn);
  
      for i:=0 to fs.Count-1 do begin
        s:=fs[i];
        StringChangeEx(s, '%APACHEROOT%', apacheroot, True);
        StringChangeEx(s, '%DOCROOT%', docroot, True);
        StringChangeEx(s, '%INSTALLDIR%', instdir, True);
        fs[i]:=s;
      end;
      fs.SaveToFile(fn);
    finally
      fs.Free;
    end;
  except
    MsgBox('failed updating: '+ExceptionParam, mbInformation, MB_OK);
  end;
end;

procedure UpdatePhpConfig;
var
  fs : TStringList;
  i  : integer;
  s  : string;
  appdata : string;
  instdir : string;
  fn : string;
begin
  try
    fs := TStringList.Create;
    try
      instdir :=  ExpandConstant('{app}');
      appdata :=  ExpandConstant('{userappdata}');

      fn := ExpandConstant(CurrentFileName);
      fs.LoadFromFile(fn);
  
      for i:=0 to fs.Count-1 do begin
        s:=fs[i];
        StringChangeEx(s, '%APPDATA%', appdata, True);
        StringChangeEx(s, '%INSTALLDIR%', instdir, True);
        fs[i]:=s;
      end;
      fs.SaveToFile(fn);
    finally
      fs.Free;
    end;
  except
    MsgBox('failed updating: '+ExceptionParam, mbInformation, MB_OK);
  end;
end;